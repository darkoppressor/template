/*
 * Copyright (c) 2019 Cheese and Bacon Games, LLC
 * This file is licensed under the MIT License.
 * See the file docs/LICENSE.txt for the full license text.
 */

def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary

if (buildAsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace 'org.cheeseandbacon.template'
    buildToolsVersion project.buildToolsVersion
    ndkVersion project.ndkVersion

    defaultConfig {
        if (buildAsApplication) {
            applicationId "org.cheeseandbacon.template"
        }

        compileSdkVersion project.targetSdkVersion
        targetSdkVersion project.targetSdkVersion
        minSdkVersion project.minSdkVersion

        versionCode project.versionCode
        versionName project.versionName

        externalNativeBuild {
            ndkBuild {
                arguments project.ndkArguments
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
    }

    compileOptions {
        sourceCompatibility project.javaVersion
        targetCompatibility project.javaVersion
    }

    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')

        // Google Play Services
        implementation 'com.google.android.gms:play-services-base:17.1.0'
        implementation 'com.google.android.gms:play-services-auth:17.0.0'
        implementation 'com.google.android.gms:play-services-games:18.0.1'
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            keyAlias 'KEY_ALIAS'
            keyPassword "KEY_PASSWORD"
            storeFile file('STORE_FILE')
            storePassword "STORE_PASSWORD"
        }

        debug {
            keyAlias 'KEY_ALIAS'
            keyPassword "KEY_PASSWORD"
            storeFile file('STORE_FILE')
            storePassword "STORE_PASSWORD"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ndk {
                debugSymbolLevel 'none'
            }
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.debug
            ndk {
                debugSymbolLevel 'full'
            }
        }
    }

    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"].dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }

    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }

        externalNativeBuild {
            ndkBuild {
                path 'jni/Android.mk'
            }
        }
    }

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.libsdl.app.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}
